;; (c) 2015 Andreas Rossberg

(module
  ;; Swap
  (func (param i32 i32) (result i32 i32)
    (return (getparam 1) (getparam 0))
  )

  ;; Test
  (func (param i32) (result i32 i32)
    (local i32 i32)
    (setlocal 0 (const.i32 1))
    (setlocal 1 (const.i32 2))
    (switch.i32 (getparam 0)
      ;; Destructure
      (case 1
        (destruct 0 1 (call 0 (getlocal 0) (getlocal 1)))
        (return (getlocal 0) (getlocal 1))
      )
      ;; Return directly
      (case 2
        (return (call 0 (getlocal 0) (getlocal 1)))
      )
      ;; Pass on to other call
      (case 3
        (return (call 0 (call 0 (getlocal 0) (getlocal 1))))
      )
      ;; Break
      (case 4
        (destruct 0 1 (label (call 0 (break 0 (getlocal 0) (getlocal 1)))))
        (return (getlocal 0) (getlocal 1))
      )
      ;; Pass on to break
      (case 5
        (return (label (call 0 (break 0 (call 0 (getlocal 0) (getlocal 1))))))
      )
      ;; Pass through block
      (case 6
        (return
          (block
            (setlocal 1 (const.i32 3))
            (call 0 (getlocal 0) (getlocal 1))
          )
        )
      )
      ;; Pass through conditional
      (case 7)
      (case 8
        (return
          (if (eq.i32 (mods.i32 (getparam 0) (const.i32 2)) (const.i32 0))
            (call 0 (getlocal 0) (getlocal 1))
            (call 0 (getlocal 1) (getlocal 0))
          )
        )
      )
      ;; Pass through switch
      (case 9)
      (case 10)
      (case 11
        (return
          (switch.i32 (getparam 0)
            (case 9 (call 0 (getlocal 0) (getlocal 1)))
            (case 10 (call 0 (getlocal 1) (getlocal 0)))
            (label (break 0 (getlocal 0) (getlocal 0)))
          )
        )
      )
      ;; Dummy default
      (return (getlocal 0) (getlocal 1))
    )
  )

  (export "test" 1)
)

(invoke "test" (const.i32 1))  ;; (2 1) : (i32 i32)
(invoke "test" (const.i32 2))  ;; (2 1) : (i32 i32)
(invoke "test" (const.i32 3))  ;; (1 2) : (i32 i32)
(invoke "test" (const.i32 4))  ;; (1 2) : (i32 i32)
(invoke "test" (const.i32 5))  ;; (2 1) : (i32 i32)
(invoke "test" (const.i32 6))  ;; (3 1) : (i32 i32)
(invoke "test" (const.i32 7))  ;; (1 2) : (i32 i32)
(invoke "test" (const.i32 8))  ;; (2 1) : (i32 i32)
(invoke "test" (const.i32 9))  ;; (2 1) : (i32 i32)
(invoke "test" (const.i32 10))  ;; (1 2) : (i32 i32)
(invoke "test" (const.i32 11))  ;; (1 1) : (i32 i32)
