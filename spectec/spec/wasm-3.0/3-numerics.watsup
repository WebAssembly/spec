;;
;; Numerics
;;

;; Conversions

def $s33_to_u32(s33) : u32  hint(show %)


;; Signed numbers

def $signed_(N, nat) : int
def $signed_(N, i) = i           -- if $(0 <= 2^(N-1))
def $signed_(N, i) = $(i - 2^N)  -- if $(2^(N-1) <= i < 2^N)

def $invsigned_(N, int) : nat    hint(show $signed_(%)^(-1)#((%)))
def $invsigned_(N, i) = j        -- if $signed_(N, j) = i


;; TODO(3, all): implement numerics internally

def $unop_(numtype, unop_(numtype), num_(numtype)) : num_(numtype)*
    hint(show %2#$_(%1,%3))
def $binop_(numtype, binop_(numtype), num_(numtype), num_(numtype)) : num_(numtype)*
    hint(show %2#$_(%1,%3, %4))
def $testop_(numtype, testop_(numtype), num_(numtype)) : num_(I32)
    hint(show %2#$_(%1,%3))
def $relop_(numtype, relop_(numtype), num_(numtype), num_(numtype)) : num_(I32)
    hint(show %2#$_(%1,%3, %4))
def $cvtop__(numtype_1, numtype_2, cvtop__(numtype_1, numtype_2), num_(numtype_1)) : num_(numtype_2)*
    hint(show %3#$__(%1,%2,%4))

def $wrap__(M, N, iN(M)) : iN(N)
def $extend__(M, N, sx, iN(M)) : iN(N)               hint(show $extend_((%,%))^(%)#((%)))
def $trunc__(M, N, sx, fN(M)) : iN(N)?               hint(show $trunc_((%,%))^(%)#((%)))
def $trunc_sat__(M, N, sx, fN(M)) : iN(N)?           hint(show $trunc__sat_((%,%))^(%)#((%)))
def $demote__(M, N, fN(M)) : fN(N)*
def $promote__(M, N, fN(M)) : fN(N)*
def $convert__(M, N, sx, iN(M)) : fN(N)              hint(show $convert_((%,%))^(%)#((%)))
def $narrow__(M, N, sx, iN(M)) : iN(N)               hint(show $narrow_((%,%))^(%)#(%))
def $reinterpret__(numtype_1, numtype_2, num_(numtype_1)) : num_(numtype_2)

def $ibits_(N, iN(N)) : bit*                         hint(show $bits_($IN(%),%))
def $fbits_(N, fN(N)) : bit*                         hint(show $bits_($FN(%),%))
def $ibytes_(N, iN(N)) : byte*                       hint(show $bytes_($IN(%),%))
def $fbytes_(N, fN(N)) : byte*                       hint(show $bytes_($FN(%),%))
def $nbytes_(numtype, num_(numtype)) : byte*         hint(show $bytes_(%,%))
def $vbytes_(vectype, vec_(vectype)) : byte*         hint(show $bytes_(%,%))
def $zbytes_(storagetype, lit_(storagetype)) : byte* hint(show $bytes_(%,%))
def $cbytes_(Cnn, lit_(Cnn)) : byte*                 hint(show $bytes_(%,%))


def $invibytes_(N, byte*) : iN(N) hint(show $bytes_($IN(%))^(-1)#((%)))
def $invfbytes_(N, byte*) : fN(N) hint(show $bytes_($FN(%))^(-1)#((%)))

def $invibytes_(N, b*) = n  -- if $ibytes_(N, n) = b*
def $invfbytes_(N, b*) = p  -- if $fbytes_(N, p) = b*


def $iadd_(N, iN(N), iN(N)) : iN(N)
def $isub_(N, iN(N), iN(N)) : iN(N)
def $imul_(N, iN(N), iN(N)) : iN(N)
def $idiv_(N, sx, iN(N), iN(N)) : iN(N)? hint(show $idiv_(%)^(%)#((%,%)))
def $irem_(N, sx, iN(N), iN(N)) : iN(N)? hint(show $irem_(%)^(%)#((%,%)))
def $inot_(N, iN(N)) : iN(N)
def $iand_(N, iN(N), iN(N)) : iN(N)
def $iandnot_(N, iN(N), iN(N)) : iN(N)
def $ior_(N, iN(N), iN(N)) : iN(N)
def $ixor_(N, iN(N), iN(N)) : iN(N)
def $ishl_(N, iN(N), u32) : iN(N)
def $ishr_(N, sx, iN(N), u32) : iN(N)    hint(show $ishr_(%)^(%)#((%,%)))
def $irotl_(N, iN(N), iN(N)) : iN(N)
def $irotr_(N, iN(N), iN(N)) : iN(N)
def $iclz_(N, iN(N)) : iN(N)
def $ictz_(N, iN(N)) : iN(N)
def $ipopcnt_(N, iN(N)) : iN(N)
def $ieqz_(N, iN(N)) : u32
def $ieq_(N, iN(N), iN(N)) : u32
def $ine_(N, iN(N), iN(N)) : u32
def $ilt_(N, sx, iN(N), iN(N)) : u32     hint(show $ilt_(%)^(%)#((%,%)))
def $igt_(N, sx, iN(N), iN(N)) : u32     hint(show $igt_(%)^(%)#((%,%)))
def $ile_(N, sx, iN(N), iN(N)) : u32     hint(show $ile_(%)^(%)#((%,%)))
def $ige_(N, sx, iN(N), iN(N)) : u32     hint(show $ige_(%)^(%)#((%,%)))
def $ibitselect_(N, iN(N), iN(N), iN(N)) : iN(N)
def $iabs_(N, iN(N)) : iN(N)
def $ineg_(N, iN(N)) : iN(N)
def $imin_(N, sx, iN(N), iN(N)) : iN(N)         hint(show $imin_(%)^(%)%((%,%)))
def $imax_(N, sx, iN(N), iN(N)) : iN(N)         hint(show $imax_(%)^(%)%((%,%)))
def $iadd_sat_(N, sx, iN(N), iN(N)) : iN(N)     hint(show $iadd__sat_(%)^(%)#((%,%)))
def $isub_sat_(N, sx, iN(N), iN(N)) : iN(N)     hint(show $isub__sat_(%)^(%)#((%,%)))
def $iavgr_(N, sx, iN(N), iN(N)) : iN(N)        hint(show $iavgr_(%)^(%)#((%,%)))
def $iq15mulr_sat_(N, sx, iN(N), iN(N)) : iN(N) hint(show $iq15mulr__sat_(%)^(%)#((%,%)))

def $fadd_(N, fN(N), fN(N)) : fN(N)*
def $fsub_(N, fN(N), fN(N)) : fN(N)*
def $fmul_(N, fN(N), fN(N)) : fN(N)*
def $fdiv_(N, fN(N), fN(N)) : fN(N)*
def $fmin_(N, fN(N), fN(N)) : fN(N)*
def $fmax_(N, fN(N), fN(N)) : fN(N)*
def $fpmin_(N, fN(N), fN(N)) : fN(N)*
def $fpmax_(N, fN(N), fN(N)) : fN(N)*
def $fcopysign_(N, fN(N), fN(N)) : fN(N)*
def $fabs_(N, fN(N)) : fN(N)*
def $fneg_(N, fN(N)) : fN(N)*
def $fsqrt_(N, fN(N)) : fN(N)*
def $fceil_(N, fN(N)) : fN(N)*
def $ffloor_(N, fN(N)) : fN(N)*
def $ftrunc_(N, fN(N)) : fN(N)*
def $fnearest_(N, fN(N)) : fN(N)*
def $feq_(N, fN(N), fN(N)) : u32
def $fne_(N, fN(N), fN(N)) : u32
def $flt_(N, fN(N), fN(N)) : u32
def $fgt_(N, fN(N), fN(N)) : u32
def $fle_(N, fN(N), fN(N)) : u32
def $fge_(N, fN(N), fN(N)) : u32

def $binop_(Inn, ADD, iN_1, iN_2) = $iadd_($sizenn(Inn), iN_1, iN_2)
def $binop_(Inn, SUB, iN_1, iN_2) = $isub_($sizenn(Inn), iN_1, iN_2)
def $binop_(Inn, MUL, iN_1, iN_2) = $imul_($sizenn(Inn), iN_1, iN_2)
def $binop_(Inn, DIV sx, iN_1, iN_2) = $list_(num_(Inn), $idiv_($sizenn(Inn), sx, iN_1, iN_2))
def $binop_(Inn, REM sx, iN_1, iN_2) = $list_(num_(Inn), $irem_($sizenn(Inn), sx, iN_1, iN_2))
def $binop_(Inn, AND, iN_1, iN_2) = $iand_($sizenn(Inn), iN_1, iN_2)
def $binop_(Inn, OR, iN_1, iN_2) = $ior_($sizenn(Inn), iN_1, iN_2)
def $binop_(Inn, XOR, iN_1, iN_2) = $ixor_($sizenn(Inn), iN_1, iN_2)
def $binop_(Inn, SHL, iN_1, iN_2) = $ishl_($sizenn(Inn), iN_1, iN_2)
def $binop_(Inn, SHR sx, iN_1, iN_2) = $ishr_($sizenn(Inn), sx, iN_1, iN_2)
def $binop_(Inn, ROTL, iN_1, iN_2) = $irotl_($sizenn(Inn), iN_1, iN_2)
def $binop_(Inn, ROTR, iN_1, iN_2) = $irotr_($sizenn(Inn), iN_1, iN_2)
def $unop_(Inn, CLZ, iN) = $iclz_($sizenn(Inn), iN)
def $unop_(Inn, CTZ, iN) = $ictz_($sizenn(Inn), iN)
def $unop_(Inn, POPCNT, iN) = $ipopcnt_($sizenn(Inn), iN)
def $unop_(Inn, EXTEND M, iN) = $extend__(M, $sizenn(Inn), S, $wrap__($sizenn(Inn), M, iN))
def $testop_(Inn, EQZ, iN) = $ieqz_($sizenn(Inn), iN)
def $relop_(Inn, EQ, iN_1, iN_2) = $ieq_($sizenn(Inn), iN_1, iN_2)
def $relop_(Inn, NE, iN_1, iN_2) = $ine_($sizenn(Inn), iN_1, iN_2)
def $relop_(Inn, LT sx, iN_1, iN_2) = $ilt_($sizenn(Inn), sx, iN_1, iN_2)
def $relop_(Inn, GT sx, iN_1, iN_2) = $igt_($sizenn(Inn), sx, iN_1, iN_2)
def $relop_(Inn, LE sx, iN_1, iN_2) = $ile_($sizenn(Inn), sx, iN_1, iN_2)
def $relop_(Inn, GE sx, iN_1, iN_2) = $ige_($sizenn(Inn), sx, iN_1, iN_2)

def $binop_(Fnn, ADD, fN_1, fN_2) = $fadd_($sizenn(Fnn), fN_1, fN_2)
def $binop_(Fnn, SUB, fN_1, fN_2) = $fsub_($sizenn(Fnn), fN_1, fN_2)
def $binop_(Fnn, MUL, fN_1, fN_2) = $fmul_($sizenn(Fnn), fN_1, fN_2)
def $binop_(Fnn, DIV, fN_1, fN_2) = $fdiv_($sizenn(Fnn), fN_1, fN_2)
def $binop_(Fnn, MIN, fN_1, fN_2) = $fmin_($sizenn(Fnn), fN_1, fN_2)
def $binop_(Fnn, MAX, fN_1, fN_2) = $fmax_($sizenn(Fnn), fN_1, fN_2)
def $binop_(Fnn, COPYSIGN, fN_1, fN_2) = $fcopysign_($sizenn(Fnn), fN_1, fN_2)

def $unop_(Fnn, ABS, fN) = $fabs_($sizenn(Fnn), fN)
def $unop_(Fnn, NEG, fN) = $fneg_($sizenn(Fnn), fN)
def $unop_(Fnn, SQRT, fN) = $fsqrt_($sizenn(Fnn), fN)
def $unop_(Fnn, CEIL, fN) = $fceil_($sizenn(Fnn), fN)
def $unop_(Fnn, FLOOR, fN) = $ffloor_($sizenn(Fnn), fN)
def $unop_(Fnn, TRUNC, fN) = $ftrunc_($sizenn(Fnn), fN)
def $unop_(Fnn, NEAREST, fN) = $fnearest_($sizenn(Fnn), fN)

def $relop_(Fnn, EQ, fN_1, fN_2) = $feq_($sizenn(Fnn), fN_1, fN_2)
def $relop_(Fnn, NE, fN_1, fN_2) = $fne_($sizenn(Fnn), fN_1, fN_2)
def $relop_(Fnn, LT, fN_1, fN_2) = $flt_($sizenn(Fnn), fN_1, fN_2)
def $relop_(Fnn, GT, fN_1, fN_2) = $fgt_($sizenn(Fnn), fN_1, fN_2)
def $relop_(Fnn, LE, fN_1, fN_2) = $fle_($sizenn(Fnn), fN_1, fN_2)
def $relop_(Fnn, GE, fN_1, fN_2) = $fge_($sizenn(Fnn), fN_1, fN_2)

def $cvtop__(Inn_1, Inn_2, EXTEND sx, iN_1) = $extend__($sizenn1(Inn_1), $sizenn2(Inn_2), sx, iN_1)
def $cvtop__(Inn_1, Inn_2, WRAP, iN_1) = $wrap__($sizenn1(Inn_1), $sizenn2(Inn_2), iN_1)
def $cvtop__(Fnn_1, Inn_2, TRUNC sx, fN_1) = $list_(num_(Inn_2), $trunc__($sizenn1(Fnn_1), $sizenn2(Inn_2), sx, fN_1))
def $cvtop__(Fnn_1, Inn_2, TRUNC_SAT sx, fN_1) = $list_(num_(Inn_2), $trunc_sat__($sizenn1(Fnn_1), $sizenn2(Inn_2), sx, fN_1))
def $cvtop__(Inn_1, Fnn_2, CONVERT sx, iN_1) = $convert__($sizenn1(Inn_1), $sizenn2(Fnn_2), sx, iN_1)
def $cvtop__(Fnn_1, Fnn_2, PROMOTE, fN_1) = $promote__($sizenn1(Fnn_1), $sizenn2(Fnn_2), fN_1)
def $cvtop__(Fnn_1, Fnn_2, DEMOTE, fN_1) = $demote__($sizenn1(Fnn_1), $sizenn2(Fnn_2), fN_1)
def $cvtop__(Inn_1, Fnn_2, REINTERPRET, iN_1) = $reinterpret__(Inn_1, Fnn_2, iN_1) -- if $size(Inn_1) = $size(Fnn_2)  ;; TODO(rossberg, 3): make implicit
def $cvtop__(Fnn_1, Inn_2, REINTERPRET, fN_1) = $reinterpret__(Fnn_1, Inn_2, fN_1) -- if $size(Fnn_1) = $size(Inn_2)  ;; TODO(rossberg, 3): make implicit


;; Packed numbers

def $lpacknum_(lanetype, num_($lunpack(lanetype))) : lane_(lanetype)
    hint(show $pack_(%,%)) hint(macro "packnum")
def $lpacknum_(numtype, c) = c
def $lpacknum_(packtype, c) = $wrap__($size($lunpack(packtype)), $psize(packtype), c)

def $lunpacknum_(lanetype, lane_(lanetype)) : num_($lunpack(lanetype))
    hint(show $unpack_(%,%)) hint(macro "unpacknum")
def $lunpacknum_(numtype, c) = c
def $lunpacknum_(packtype, c) = $extend__($psize(packtype), $size($lunpack(packtype)), U, c)

def $cpacknum_(storagetype, lit_($cunpack(storagetype))) : lit_(storagetype)
    hint(show $pack_(%,%)) hint(macro "packnum")
def $cpacknum_(consttype, c) = c
def $cpacknum_(packtype, c) = $wrap__($size($lunpack(packtype)), $psize(packtype), c)

def $cunpacknum_(storagetype, lit_(storagetype)) : lit_($cunpack(storagetype))
    hint(show $unpack_(%,%)) hint(macro "unpacknum")
def $cunpacknum_(consttype, c) = c
def $cunpacknum_(packtype, c) = $extend__($psize(packtype), $size($lunpack(packtype)), U, c)


;; Vectors

;; TODO(rossberg, 4): avoid hardcoding v128

def $lanes_(shape, vec_(V128)) : lane_($lanetype(shape))*

def $invlanes_(shape, lane_($lanetype(shape))*) : vec_(V128)
    hint(show $lanes_(%)^(-1)#((%)))
def $invlanes_(sh, c*) = vc  -- if c* = $lanes_(sh, vc)

;; TODO(3, rossberg): somehow enable merging cases
def $half__(shape_1, shape_2, half__(shape_1, shape_2), nat, nat) : nat hint(show $half(%3,%4,%5))
def $half__(Jnn_1 X M_1, Jnn_2 X M_2, LOW, i, j) = i
def $half__(Jnn_1 X M_1, Jnn_2 X M_2, HIGH, i, j) = j
def $half__(Lnn_1 X M_1, Fnn_2 X M_2, LOW, i, j) = i

def $vvunop_(vectype, vvunop, vec_(vectype)) : vec_(vectype)*
    hint(show %2#$_(%1,%3))
def $vvbinop_(vectype, vvbinop, vec_(vectype), vec_(vectype)) : vec_(vectype)*
    hint(show %2#$_(%1,%3,%4))
def $vvternop_(vectype, vvternop, vec_(vectype), vec_(vectype), vec_(vectype)) : vec_(vectype)*
    hint(show %2#$_(%1,%3,%4,%5))

def $vvunop_(V128, NOT, v128) = $inot_($vsize(V128), v128)
def $vvbinop_(V128, AND, v128_1, v128_2) = $iand_($vsize(V128), v128_1, v128_2)
def $vvbinop_(V128, ANDNOT, v128_1, v128_2) = $iandnot_($vsize(V128), v128_1, v128_2)
def $vvbinop_(V128, OR, v128_1, v128_2) = $ior_($vsize(V128), v128_1, v128_2)
def $vvbinop_(V128, XOR, v128_1, v128_2) = $ixor_($vsize(V128), v128_1, v128_2)
def $vvternop_(V128, BITSELECT, v128_1, v128_2, v128_3) = $ibitselect_($vsize(V128), v128_1, v128_2, v128_3)

;; TODO(2, rossberg): rename these to mapunop etc?
def $vunop_(shape, vunop_(shape), vec_(V128)) : vec_(V128)*
    hint(show %2#$_(%1,%3))
def $vbinop_(shape, vbinop_(shape), vec_(V128), vec_(V128)) : vec_(V128)*
    hint(show %2#$_(%1,%3,%4))
def $vrelop_(shape, vrelop_(shape), vec_(V128), vec_(V128)) : vec_(V128)
    hint(show %2#$_(%1,%3,%4))
def $vcvtop__(shape_1, shape_2, vcvtop__(shape_1, shape_2), lane_($lanetype(shape_1))) : lane_($lanetype(shape_2))*
    hint(show %3#$__(%1,%2)^(%4)#((%5)))

def $ivunop_(shape, def $f_(N, iN(N)) : iN(N), vec_(V128)) : vec_(V128)*
def $ivunop_(Jnn X M, def $f_, vN_1) = $invlanes_(Jnn X M, c*)
    -- if c_1* = $lanes_(Jnn X M, vN_1)
    -- if c* = $f_($lsizenn(Jnn), c_1)*

def $vunop_(Jnn X M, ABS, vN_1) = $ivunop_(Jnn X M, $iabs_, vN_1)
def $vunop_(Jnn X M, NEG, vN_1) = $ivunop_(Jnn X M, $ineg_, vN_1)
def $vunop_(Jnn X M, POPCNT, vN_1) = $ivunop_(Jnn X M, $ipopcnt_, vN_1)

def $ivbinop_(shape, def $f_(N, iN(N), iN(N)) : iN(N), vec_(V128), vec_(V128)) : vec_(V128)*
def $ivbinop_(Jnn X M, def $f_, vN_1, vN_2) = $invlanes_(Jnn X M, c*)
    -- if c_1* = $lanes_(Jnn X M, vN_1)
    -- if c_2* = $lanes_(Jnn X M, vN_2)
    -- if c* = $f_($lsizenn(Jnn), c_1, c_2)*

def $ivbinopsx_(shape, def $f_(N, sx, iN(N), iN(N)) : iN(N), sx, vec_(V128), vec_(V128)) : vec_(V128)*
def $ivbinopsx_(Jnn X M, def $f_, sx, vN_1, vN_2) = $invlanes_(Jnn X M, c*)
    -- if c_1* = $lanes_(Jnn X M, vN_1)
    -- if c_2* = $lanes_(Jnn X M, vN_2)
    -- if c* = $f_($lsizenn(Jnn), sx, c_1, c_2)*

def $vbinop_(Jnn X M, ADD, vN_1, vN_2) = $ivbinop_(Jnn X M, $iadd_, vN_1, vN_2)
def $vbinop_(Jnn X M, SUB, vN_1, vN_2) = $ivbinop_(Jnn X M, $isub_, vN_1, vN_2)
def $vbinop_(Jnn X M, MUL, vN_1, vN_2) = $ivbinop_(Jnn X M, $imul_, vN_1, vN_2)
def $vbinop_(Jnn X M, ADD_SAT sx, vN_1, vN_2) = $ivbinopsx_(Jnn X M, $iadd_sat_, sx, vN_1, vN_2)
def $vbinop_(Jnn X M, SUB_SAT sx, vN_1, vN_2) = $ivbinopsx_(Jnn X M, $isub_sat_, sx, vN_1, vN_2)
def $vbinop_(Jnn X M, MIN sx, vN_1, vN_2) = $ivbinopsx_(Jnn X M, $imin_, sx, vN_1, vN_2)
def $vbinop_(Jnn X M, MAX sx, vN_1, vN_2) = $ivbinopsx_(Jnn X M, $imax_, sx, vN_1, vN_2)
def $vbinop_(Jnn X M, AVGR U, vN_1, vN_2) = $ivbinopsx_(Jnn X M, $iavgr_, U, vN_1, vN_2)
def $vbinop_(Jnn X M, Q15MULR_SAT S, vN_1, vN_2) = $ivbinopsx_(Jnn X M, $iq15mulr_sat_, S, vN_1, vN_2)

def $ivrelop_(shape, def $f_(N, iN(N), iN(N)) : u32, vec_(V128), vec_(V128)) : vec_(V128)
def $ivrelop_(Jnn X M, def $f_, vN_1, vN_2) = $invlanes_(Jnn X M, c*)
    -- if c_1* = $lanes_(Jnn X M, vN_1)
    -- if c_2* = $lanes_(Jnn X M, vN_2)
    -- if c* = $extend__(1, $lsizenn(Jnn), S, $f_($lsizenn(Jnn), c_1, c_2))*

def $ivrelopsx_(shape, def $f_(N, sx, iN(N), iN(N)) : u32, sx, vec_(V128), vec_(V128)) : vec_(V128)
def $ivrelopsx_(Jnn X M, def $f_, sx, vN_1, vN_2) = $invlanes_(Jnn X M, c*)
    -- if c_1* = $lanes_(Jnn X M, vN_1)
    -- if c_2* = $lanes_(Jnn X M, vN_2)
    -- if c* = $extend__(1, $lsizenn(Jnn), S, $f_($lsizenn(Jnn), sx, c_1, c_2))*

def $vrelop_(Jnn X M, EQ, vN_1, vN_2) = $ivrelop_(Jnn X M, $ieq_, vN_1, vN_2)
def $vrelop_(Jnn X M, NE, vN_1, vN_2) = $ivrelop_(Jnn X M, $ine_, vN_1, vN_2)
def $vrelop_(Jnn X M, LT sx, vN_1, vN_2) = $ivrelopsx_(Jnn X M, $ilt_, sx, vN_1, vN_2)
def $vrelop_(Jnn X M, GT sx, vN_1, vN_2) = $ivrelopsx_(Jnn X M, $igt_, sx, vN_1, vN_2)
def $vrelop_(Jnn X M, LE sx, vN_1, vN_2) = $ivrelopsx_(Jnn X M, $ile_, sx, vN_1, vN_2)
def $vrelop_(Jnn X M, GE sx, vN_1, vN_2) = $ivrelopsx_(Jnn X M, $ige_, sx, vN_1, vN_2)

def $fvunop_(shape, def $f_(N, fN(N)) : fN(N)*, vec_(V128)) : vec_(V128)*
def $fvunop_(Fnn X M, def $f_, vN_1) = $invlanes_(Fnn X M, c*)*
    -- if c_1* = $lanes_(Fnn X M, vN_1)
    -- if c** = $setproduct_(lane_(Fnn), $f_($sizenn(Fnn), c_1)*)

def $vunop_(Fnn X M, ABS, vN_1) = $fvunop_(Fnn X M, $fabs_, vN_1)
def $vunop_(Fnn X M, NEG, vN_1) = $fvunop_(Fnn X M, $fneg_, vN_1)
def $vunop_(Fnn X M, SQRT, vN_1) = $fvunop_(Fnn X M, $fsqrt_, vN_1)
def $vunop_(Fnn X M, CEIL, vN_1) = $fvunop_(Fnn X M, $fceil_, vN_1)
def $vunop_(Fnn X M, FLOOR, vN_1) = $fvunop_(Fnn X M, $ffloor_, vN_1)
def $vunop_(Fnn X M, TRUNC, vN_1) = $fvunop_(Fnn X M, $ftrunc_, vN_1)
def $vunop_(Fnn X M, NEAREST, vN_1) = $fvunop_(Fnn X M, $fnearest_, vN_1)

def $fvbinop_(shape, def $f_(N, fN(N), fN(N)) : fN(N)*, vec_(V128), vec_(V128)) : vec_(V128)*
def $fvbinop_(Fnn X M, def $f_, vN_1, vN_2) = $invlanes_(Fnn X M, c*)*
    -- if c_1* = $lanes_(Fnn X M, vN_1)
    -- if c_2* = $lanes_(Fnn X M, vN_2)
    -- if c** = $setproduct_(lane_(Fnn), $f_($sizenn(Fnn), c_1, c_2)*)

def $vbinop_(Fnn X M, ADD, vN_1, vN_2) = $fvbinop_(Fnn X M, $fadd_, vN_1, vN_2)
def $vbinop_(Fnn X M, SUB, vN_1, vN_2) = $fvbinop_(Fnn X M, $fsub_, vN_1, vN_2)
def $vbinop_(Fnn X M, MUL, vN_1, vN_2) = $fvbinop_(Fnn X M, $fmul_, vN_1, vN_2)
def $vbinop_(Fnn X M, DIV, vN_1, vN_2) = $fvbinop_(Fnn X M, $fdiv_, vN_1, vN_2)
def $vbinop_(Fnn X M, MIN, vN_1, vN_2) = $fvbinop_(Fnn X M, $fmin_, vN_1, vN_2)
def $vbinop_(Fnn X M, MAX, vN_1, vN_2) = $fvbinop_(Fnn X M, $fmax_, vN_1, vN_2)
def $vbinop_(Fnn X M, PMIN, vN_1, vN_2) = $fvbinop_(Fnn X M, $fpmin_, vN_1, vN_2)
def $vbinop_(Fnn X M, PMAX, vN_1, vN_2) = $fvbinop_(Fnn X M, $fpmax_, vN_1, vN_2)

def $fvrelop_(shape, def $f_(N, fN(N), fN(N)) : u32, vec_(V128), vec_(V128)) : vec_(V128)
def $fvrelop_(Fnn X M, def $f_, vN_1, vN_2) = $invlanes_(Inn X M, c*)
    -- if c_1* = $lanes_(Fnn X M, vN_1)
    -- if c_2* = $lanes_(Fnn X M, vN_2)
    -- if c* = $extend__(1, $sizenn(Fnn), S, $f_($sizenn(Fnn), c_1, c_2))*
    -- if $size(Inn) = $size(Fnn)  ;; TODO(rossberg, 3): make implicit

def $vrelop_(Fnn X M, EQ, vN_1, vN_2) = $fvrelop_(Fnn X M, $feq_, vN_1, vN_2)
def $vrelop_(Fnn X M, NE, vN_1, vN_2) = $fvrelop_(Fnn X M, $fne_, vN_1, vN_2)
def $vrelop_(Fnn X M, LT, vN_1, vN_2) = $fvrelop_(Fnn X M, $flt_, vN_1, vN_2)
def $vrelop_(Fnn X M, GT, vN_1, vN_2) = $fvrelop_(Fnn X M, $fgt_, vN_1, vN_2)
def $vrelop_(Fnn X M, LE, vN_1, vN_2) = $fvrelop_(Fnn X M, $fle_, vN_1, vN_2)
def $vrelop_(Fnn X M, GE, vN_1, vN_2) = $fvrelop_(Fnn X M, $fge_, vN_1, vN_2)

def $vcvtop__(Jnn_1 X M_1, Jnn_2 X M_2, EXTEND sx, iN_1) = iN_2
    -- if iN_2 = $extend__($lsizenn1(Jnn_1), $lsizenn2(Jnn_2), sx, iN_1)
def $vcvtop__(Jnn_1 X M_1, Fnn_2 X M_2, CONVERT sx, iN_1) = fN_2
    -- if fN_2 = $convert__($lsizenn1(Jnn_1), $lsizenn2(Fnn_2), sx, iN_1)
def $vcvtop__(Fnn_1 X M_1, Inn_2 X M_2, TRUNC_SAT sx, fN_1) = $list_(lane_(Inn_2), iN_2?)
    -- if iN_2? = $trunc_sat__($lsizenn1(Fnn_1), $lsizenn2(Inn_2), sx, fN_1)
def $vcvtop__(Fnn_1 X M_1, Fnn_2 X M_2, DEMOTE, fN_1) = fN_2*
    -- if fN_2* = $demote__($lsizenn1(Fnn_1), $lsizenn2(Fnn_2), fN_1)
def $vcvtop__(Fnn_1 X M_1, Fnn_2 X M_2, PROMOTE, fN_1) = fN_2*
    -- if fN_2* = $promote__($lsizenn1(Fnn_1), $lsizenn2(Fnn_2), fN_1)

def $vextunop__(ishape_1, ishape_2, vextunop__(ishape_1, ishape_2), vec_(V128)) : vec_(V128)
    hint(show %3#$__(%1,%2,%4))
def $vextbinop__(ishape_1, ishape_2, vextbinop__(ishape_1, ishape_2), vec_(V128), vec_(V128)) : vec_(V128)
    hint(show %3#$__(%1,%2,%4,%5))

def $vextunop__(Jnn_1 X M_1, Jnn_2 X M_2, EXTADD_PAIRWISE sx, c_1) = c
  -- var cj_1 : iN($lsize(Jnn_2))
  -- var cj_2 : iN($lsize(Jnn_2))
  -- if ci* = $lanes_(Jnn_1 X M_1, c_1)
  -- if $concat_(iN($lsizenn2(Jnn_2)), (cj_1 cj_2)*) = $extend__($lsizenn1(Jnn_1), $lsizenn2(Jnn_2), sx, ci)*
  -- if c = $invlanes_(Jnn_2 X M_2, $iadd_($lsizenn2(Jnn_2), cj_1, cj_2)*)

def $vextbinop__(Jnn_1 X M_1, Jnn_2 X M_2, EXTMUL sx half, c_1, c_2) = c
  -- if ci_1* = $lanes_(Jnn_1 X M_1, c_1)[$half__(Jnn_1 X M_1, Jnn_2 X M_2, half, 0, M_2) : M_2]
  -- if ci_2* = $lanes_(Jnn_1 X M_1, c_2)[$half__(Jnn_1 X M_1, Jnn_2 X M_2, half, 0, M_2) : M_2]
  -- if c = $invlanes_(Jnn_2 X M_2, $imul_($lsizenn2(Jnn_2), $extend__($lsizenn1(Jnn_1), $lsizenn2(Jnn_2), sx, ci_1), $extend__($lsizenn1(Jnn_1), $lsizenn2(Jnn_2), sx, ci_2))*)
def $vextbinop__(Jnn_1 X M_1, Jnn_2 X M_2, DOT S, c_1, c_2) = c
  -- var cj_1 : iN($lsize(Jnn_2))
  -- var cj_2 : iN($lsize(Jnn_2))
  -- if ci_1* = $lanes_(Jnn_1 X M_1, c_1)
  -- if ci_2* = $lanes_(Jnn_1 X M_1, c_2)
  -- if $concat_(iN($lsizenn2(Jnn_2)), (cj_1 cj_2)*) = $imul_($lsizenn2(Jnn_2), $extend__($lsizenn1(Jnn_1), $lsizenn2(Jnn_2), S, ci_1), $extend__($lsizenn1(Jnn_1), $lsizenn2(Jnn_2), S, ci_2))*
  -- if c = $invlanes_(Jnn_2 X M_2, $iadd_($lsizenn2(Jnn_2), cj_1, cj_2)*)

;; TODO(3, rossberg): refactor for consistency?
def $vshiftop_(ishape, vshiftop_(ishape), lane_($lanetype(ishape)), u32) : lane_($lanetype(ishape))
    hint(show %2#$_(%1)#(%3, %4))

def $vshiftop_(Jnn X M, SHL, lane, n) = $ishl_($lsizenn(Jnn), lane, n)
def $vshiftop_(Jnn X M, SHR sx, lane, n) = $ishr_($lsizenn(Jnn), sx, lane, n)
