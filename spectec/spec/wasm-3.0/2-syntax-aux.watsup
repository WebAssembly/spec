;;
;; Auxiliary definitions on Indices
;;

;; Sequence difference

def $setminus(idx*, idx*) : idx*  hint(show %\%)
def $setminus1(idx, idx*) : idx*

def $setminus(eps, y*) = eps
def $setminus(x_1 x*, y*) = $setminus1(x_1, y*) $setminus(x*, y*)

def $setminus1(x, eps) = x
def $setminus1(x, y_1 y*) = eps                -- if x = y_1
def $setminus1(x, y_1 y*) = $setminus1(x, y*)  -- otherwise


;; Free indices

def $free_dataidx_instr(instr) : dataidx*  hint(show $free_dataidx(%))
def $free_dataidx_instr(MEMORY.INIT x y) = y
def $free_dataidx_instr(DATA.DROP x) = x
def $free_dataidx_instr(in) = eps

def $free_dataidx_instrs(instr*) : dataidx*  hint(show $free_dataidx(%))
def $free_dataidx_instrs(eps) = eps
def $free_dataidx_instrs(instr instr'*) = $free_dataidx_instr(instr) $free_dataidx_instrs(instr'*)

def $free_dataidx_expr(expr) : dataidx*  hint(show $free_dataidx(%))
def $free_dataidx_expr(in*) = $free_dataidx_instrs(in*)

def $free_dataidx_func(func) : dataidx*  hint(show $free_dataidx(%))
def $free_dataidx_func(FUNC x loc* e) = $free_dataidx_expr(e)

def $free_dataidx_funcs(func*) : dataidx*  hint(show $free_dataidx(%))
def $free_dataidx_funcs(eps) = eps
def $free_dataidx_funcs(func func'*) = $free_dataidx_func(func) $free_dataidx_funcs(func'*)


;;
;; Auxiliary definitions on Types
;;

;; Size

;; def |I n| = |F n| = |V n| = n        ;; ????

;;def $size(numtype) : nat       hint(show |%|)
;;def $vsize(vegtype) : nat      hint(show |%|)
;;def $psize(packtype) : nat     hint(show |%|)
;;def $lsize(lanetype) : nat     hint(show |%|)
;;def $zsize(storagetype) : nat  hint(show |%|) hint(partial)

def $size(I32) = 32
def $size(I64) = 64
def $size(F32) = 32
def $size(F64) = 64

def $isize(Inn) = $size(Inn)

def $vsize(V128) = 128

def $psize(I8) = 8
def $psize(I16) = 16

def $lsize(numtype) = $size(numtype)
def $lsize(packtype) = $psize(packtype)

def $zsize(numtype) = $size(numtype)
def $zsize(vectype) = $vsize(vectype)
def $zsize(packtype) = $psize(packtype)

def $IN(N) : numtype  hint(show I#%) hint(macro "numtype")
def $IN(32) = I32
def $IN(64) = I64

def $FN(N) : numtype  hint(show F#%) hint(macro "numtype")
def $FN(32) = F32
def $FN(64) = F64


;; Unpacking

def $lunpack(lanetype) : numtype  hint(show $unpack(%))
def $lunpack(numtype) = numtype
def $lunpack(packtype) = I32

def $unpack(storagetype) : valtype
def $unpack(valtype) = valtype
def $unpack(packtype) = I32

def $nunpack(storagetype) : numtype  hint(show $unpack(%)) hint(partial)
def $nunpack(numtype) = numtype
def $nunpack(packtype) = I32

def $vunpack(storagetype) : vectype  hint(show $unpack(%)) hint(partial)
def $vunpack(vectype) = vectype

def $cunpack(storagetype) : consttype hint(show $unpack(%)) hint(partial)
def $cunpack(consttype) = consttype
def $cunpack(packtype) = I32
def $cunpack(lanetype) = $lunpack(lanetype)  ;; HACK

def $sx(storagetype) : sx?  hint(show $sx(%))
def $sx(consttype) = eps
def $sx(packtype) = S


;; Const

def $const(consttype, lit_(consttype)): instr  hint(show %.CONST %)
def $const(numtype, c) = (CONST numtype c)
def $const(vectype, c) = (VCONST vectype c)


;; Shapes

;;def $lanetype(shape) : lanetype
def $lanetype(Lnn X N) = Lnn

;;def $dim(shape) : nat
def $dim(Lnn X N) = N

;;def $shsize(shape) : nat
def $shsize(Lnn X N) = $($lsize(Lnn) * N)

def $unpackshape(shape) : numtype  hint(show $unpack(%))
def $unpackshape(Lnn X N) = $lunpack(Lnn)


;; Type difference

def $diffrt(reftype, reftype) : reftype  hint(show %\%)

def $diffrt((REF nul_1 ht_1), (REF NULL ht_2)) = (REF eps ht_1)
def $diffrt((REF nul_1 ht_1), (REF eps ht_2)) = (REF nul_1 ht_1)


;; Injection

syntax typevar = _IDX typeidx | REC nat
var xx : typevar
var yy : typeuse hint(show y)
var tu : typeuse

def $idx(typeidx) : typevar  hint(show %)
def $idx(x) = _IDX x


;; Substitution

def $subst_typevar(typevar, typevar*, typeuse*) : typeuse             hint(show %#`[%:=%])
def $subst_typeuse(typeuse, typevar*, typeuse*) : typeuse             hint(show %#`[%:=%])

def $subst_numtype(numtype, typevar*, typeuse*) : numtype             hint(show %#`[%:=%])
def $subst_vectype(vectype, typevar*, typeuse*) : vectype             hint(show %#`[%:=%])
def $subst_heaptype(heaptype, typevar*, typeuse*) : heaptype          hint(show %#`[%:=%])
def $subst_reftype(reftype, typevar*, typeuse*) : reftype             hint(show %#`[%:=%])
def $subst_valtype(valtype, typevar*, typeuse*) : valtype             hint(show %#`[%:=%])

def $subst_packtype(packtype, typevar*, typeuse*) : packtype          hint(show %#`[%:=%])
def $subst_storagetype(storagetype, typevar*, typeuse*) : storagetype hint(show %#`[%:=%])
def $subst_fieldtype(fieldtype, typevar*, typeuse*) : fieldtype       hint(show %#`[%:=%])

def $subst_comptype(comptype, typevar*, typeuse*) : comptype          hint(show %#`[%:=%])
def $subst_subtype(subtype, typevar*, typeuse*) : subtype             hint(show %#`[%:=%])
def $subst_rectype(rectype, typevar*, typeuse*) : rectype             hint(show %#`[%:=%])
def $subst_deftype(deftype, typevar*, typeuse*) : deftype             hint(show %#`[%:=%])

def $subst_globaltype(globaltype, typevar*, typeuse*) : globaltype    hint(show %#`[%:=%])
def $subst_functype(functype, typevar*, typeuse*) : functype          hint(show %#`[%:=%])
def $subst_tabletype(tabletype, typevar*, typeuse*) : tabletype       hint(show %#`[%:=%])
def $subst_memtype(memtype, typevar*, typeuse*) : memtype             hint(show %#`[%:=%])

def $subst_externtype(externtype, typevar*, typeuse*) : externtype    hint(show %#`[%:=%])


def $subst_typevar(xx, eps, eps) = xx
def $subst_typevar(xx, xx_1 xx'*, tu_1 tu'*) = tu_1                            -- if xx = xx_1
def $subst_typevar(xx, xx_1 xx'*, tu_1 tu'*) = $subst_typevar(xx, xx'*, tu'*)  -- otherwise

def $subst_typeuse(xx', xx*, tu*) = $subst_typevar(xx', xx*, tu*)
def $subst_typeuse(dt, xx*, tu*) = $subst_deftype(dt, xx*, tu*)

def $subst_numtype(nt, xx*, tu*) = nt
def $subst_vectype(vt, xx*, tu*) = vt

def $subst_heaptype(xx', xx*, tu*) = $subst_typevar(xx', xx*, tu*)
def $subst_heaptype(dt, xx*, tu*) = $subst_deftype(dt, xx*, tu*)
def $subst_heaptype(ht, xx*, tu*) = ht  -- otherwise

def $subst_reftype((REF nul ht), xx*, tu*) = REF nul $subst_heaptype(ht, xx*, tu*)

def $subst_valtype(nt, xx*, tu*) = $subst_numtype(nt, xx*, tu*)
def $subst_valtype(vt, xx*, tu*) = $subst_vectype(vt, xx*, tu*)
def $subst_valtype(rt, xx*, tu*) = $subst_reftype(rt, xx*, tu*)
def $subst_valtype(BOT, xx*, tu*) = BOT

def $subst_packtype(pt, xx*, tu*) = pt

def $subst_storagetype(t, xx*, tu*) = $subst_valtype(t, xx*, tu*)
def $subst_storagetype(pt, xx*, tu*) = $subst_packtype(pt, xx*, tu*)

def $subst_fieldtype((mut zt), xx*, tu*) = mut $subst_storagetype(zt, xx*, tu*)

def $subst_comptype((STRUCT yt*), xx*, tu*) = STRUCT $subst_fieldtype(yt, xx*, tu*)*
def $subst_comptype((ARRAY yt), xx*, tu*) = ARRAY $subst_fieldtype(yt, xx*, tu*)
def $subst_comptype((FUNC ft), xx*, tu*) = FUNC $subst_functype(ft, xx*, tu*)

def $subst_subtype((SUB fin tu'* ct), xx*, tu*) =
  SUB fin $subst_typeuse(tu', xx*, tu*)* $subst_comptype(ct, xx*, tu*)

def $subst_rectype((REC st*), xx*, tu*) = REC $subst_subtype(st, xx*, tu*)*

def $subst_deftype((DEF qt i), xx*, tu*) = DEF $subst_rectype(qt, xx*, tu*) i

def $subst_globaltype((mut t), xx*, tu*) = mut $subst_valtype(t, xx*, tu*)
def $subst_functype((t_1* -> t_2*), xx*, tu*) = $subst_valtype(t_1, xx*, tu*)* -> $subst_valtype(t_2, xx*, tu*)*
def $subst_memtype((lim I8), xx*, tu*) = lim I8
def $subst_tabletype((lim rt), xx*, tu*) = lim $subst_reftype(rt, xx*, tu*)

def $subst_externtype((FUNC dt), xx*, tu*) = FUNC $subst_deftype(dt, xx*, tu*)
def $subst_externtype((GLOBAL gt), xx*, tu*) = GLOBAL $subst_globaltype(gt, xx*, tu*)
def $subst_externtype((TABLE tt), xx*, tu*) = TABLE $subst_tabletype(tt, xx*, tu*)
def $subst_externtype((MEM mt), xx*, tu*) = MEM $subst_memtype(mt, xx*, tu*)


def $subst_all_reftype(reftype, heaptype*) : reftype  hint(show %#`[:=%])
def $subst_all_deftype(deftype, heaptype*) : deftype  hint(show %#`[:=%])

def $subst_all_reftype(rt, tu^n) = $subst_reftype(rt, $idx(i)^(i<n), tu^n)
def $subst_all_deftype(dt, tu^n) = $subst_deftype(dt, $idx(i)^(i<n), tu^n)

def $subst_all_deftypes(deftype*, heaptype*) : deftype*  hint(show %#`[:=%])

def $subst_all_deftypes(eps, tu*) = eps
def $subst_all_deftypes(dt_1 dt*, tu*) = $subst_all_deftype(dt_1, tu*) $subst_all_deftypes(dt*, tu*)


;; Rolling and Unrolling

def $rollrt(typeidx, rectype) : rectype  hint(show $roll_(%, %))   hint(macro "rollrt")
def $unrollrt(rectype) : rectype         hint(show $unroll(%))     hint(macro "unrollrt")
def $rolldt(typeidx, rectype) : deftype* hint(show $roll_(%)*#(%)) hint(macro "rolldt")
def $unrolldt(deftype) : subtype         hint(show $unroll(%))     hint(macro "unrolldt")
def $expanddt(deftype) : comptype        hint(show $expand(%))     hint(macro "expanddt")

;; TODO: in general, multi-dimensional use of dimensioned vars is ambiguous;
;; for example, x** with dimension x* could be x1 x2 x1 x2 of x1 x1 x2 x2.
;; For now, we avoid to reuse the dimension var in exact same form
def $rollrt(x, REC st^n) = REC ($subst_subtype(st, ($idx($(x + i)))^(i<n), (REC i)^(i<n)))^n
def $unrollrt(REC st^n) = REC ($subst_subtype(st, (REC i)^(i<n), (DEF qt i)^(i<n)))^n
  -- if qt = REC st^n

def $rolldt(x, qt) = (DEF (REC st^n) i)^(i<n)  -- if $rollrt(x, qt) = REC st^n
def $unrolldt(DEF qt i) = st*[i]               -- if $unrollrt(qt) = REC st*

def $expanddt(dt) = ct                         -- if $unrolldt(dt) = SUB fin tu* ct

relation Expand: deftype ~~ comptype hint(macro "%expanddt")
rule Expand: dt ~~ ct                          -- if $expanddt(dt) = ct


;; Projections

;; TODO: use built-in notation
def $funcsxt(externtype*) : deftype*       hint(show $funcs(%))   hint(macro "funcsxt")
def $globalsxt(externtype*) : globaltype*  hint(show $globals(%)) hint(macro "globalsxt")
def $tablesxt(externtype*) : tabletype*    hint(show $tables(%))  hint(macro "tablesxt")
def $memsxt(externtype*) : memtype*        hint(show $mems(%))    hint(macro "memsxt")

def $funcsxt(eps) = eps
def $funcsxt((FUNC dt) xt*) = dt $funcsxt(xt*)
def $funcsxt(externtype xt*) = $funcsxt(xt*)  -- otherwise

def $globalsxt(eps) = eps
def $globalsxt((GLOBAL gt) xt*) = gt $globalsxt(xt*)
def $globalsxt(externtype xt*) = $globalsxt(xt*)  -- otherwise

def $tablesxt(eps) = eps
def $tablesxt((TABLE tt) xt*) = tt $tablesxt(xt*)
def $tablesxt(externtype xt*) = $tablesxt(xt*)  -- otherwise

def $memsxt(eps) = eps
def $memsxt((MEM mt) xt*) = mt $memsxt(xt*)
def $memsxt(externtype xt*) = $memsxt(xt*)  -- otherwise



;;
;; Auxiliary definitions on Instructions
;;

;; Shorthands for Instructions

def $memarg0 : memarg  hint(show )
def $memarg0 = {ALIGN 0, OFFSET 0}
