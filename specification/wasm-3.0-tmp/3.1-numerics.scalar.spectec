;;
;; Numeric Primitives
;;

;; Conversions

def $s33_to_u32(s33) : u32  hint(show %)


;; Representation

def $ibits_(N, iN(N)) : bit*                         hint(show $bits_($IN(%),%)) hint(inverse $inv_ibits_)
def $fbits_(N, fN(N)) : bit*                         hint(show $bits_($FN(%),%)) hint(inverse $inv_ibits_)
def $ibytes_(N, iN(N)) : byte*                       hint(show $bytes_($IN(%),%)) hint(inverse $inv_ibytes_)
def $fbytes_(N, fN(N)) : byte*                       hint(show $bytes_($FN(%),%)) hint(inverse $inv_fbytes_)
def $nbytes_(numtype, num_(numtype)) : byte*         hint(show $bytes_(%,%)) hint(inverse $inv_nbytes_)
def $vbytes_(vectype, vec_(vectype)) : byte*         hint(show $bytes_(%,%)) hint(inverse $inv_vbytes_)
def $zbytes_(storagetype, lit_(storagetype)) : byte* hint(show $bytes_(%,%)) hint(inverse $inv_zbytes_)
def $cbytes_(Cnn, lit_(Cnn)) : byte*                 hint(show $bytes_(%,%)) hint(inverse $inv_cbytes_)

def $inv_ibits_(N, bit*) : iN(N)                     hint(show $bits_($IN(%))^$(-1)#((%)))
def $inv_fbits_(N, bit*) : fN(N)                     hint(show $bits_($FN(%))^$(-1)#((%)))
def $inv_ibytes_(N, byte*) : iN(N)                   hint(show $bytes_($IN(%))^$(-1)#((%)))
def $inv_fbytes_(N, byte*) : fN(N)                   hint(show $bytes_($FN(%))^$(-1)#((%)))
def $inv_nbytes_(numtype, byte*) : num_(numtype)     hint(show $bytes_(%)^$(-1)#((%)))
def $inv_vbytes_(vectype, byte*) : vec_(vectype)     hint(show $bytes_(%)^$(-1)#((%)))
def $inv_zbytes_(storagetype, byte*) : lit_(storagetype)  hint(show $bytes_(%)^$(-1)#((%)))
def $inv_cbytes_(Cnn, byte*) : lit_(Cnn)             hint(show $bytes_(%)^$(-1)#((%)))

;; TODO(3, rossberg): implement numerics internally
def $ibits_  hint(builtin)
def $fbits_  hint(builtin)
def $ibytes_ hint(builtin)
def $fbytes_ hint(builtin)
def $nbytes_ hint(builtin)
def $vbytes_ hint(builtin)
def $zbytes_ hint(builtin)
def $cbytes_ hint(builtin)

def $inv_ibits_ hint(builtin)
def $inv_fbits_ hint(builtin)
def $inv_ibytes_ hint(builtin)
def $inv_fbytes_ hint(builtin)
def $inv_nbytes_ hint(builtin)
def $inv_vbytes_ hint(builtin)
def $inv_zbytes_ hint(builtin)
def $inv_cbytes_ hint(builtin)


;; Signed numbers

def $signed_(N, nat) : int hint(inverse $inv_signed_)
def $signed_(N, i) = i           -- if $(i < 2^(N-1))
def $signed_(N, i) = $(i - 2^N)  -- if $(2^(N-1) <= i < 2^N)

def $inv_signed_(N, int) : nat    hint(show $signed_(%)^(-1)#((%)))
def $inv_signed_(N, i) = i          -- if $(0 <= i < 2^(N-1))
def $inv_signed_(N, i) = $(i + 2^N) -- if $(-2^(N-1) <= i < 0)


def $sx(storagetype) : sx?  hint(show $sx(%))
def $sx(consttype) = eps
def $sx(packtype) = S


;; Construction

def $zero(lanetype) : lane_(lanetype) hint(show 0)
def $zero(Jnn) = 0
def $zero(Fnn) = $fzero($size(Fnn))

def $bool(bool) : nat
def $bool(false) = 0
def $bool(true) = 1

def $truncz(rat) : int
def $truncz hint(builtin)
;;def $truncz(+-q) = +-n  -- if n <- nat /\ $(q - 1) < n <= q

def $ceilz(rat) : int hint(show $ceil(%)) hint(macro none)
def $ceilz hint(builtin)
;;def $ceilz(+-q) = +-n  -- if n <- nat /\ q <= n < $(q + 1)


;; Saturation

def $sat_u_(N, int) : nat           hint(show $sat__u_(%,%))
def $sat_u_(N, i) = 0               -- if i < 0
def $sat_u_(N, i) = $(2^N - 1)      -- if i > $(2^N - 1)
def $sat_u_(N, i) = i               -- otherwise

def $sat_s_(N, int) : int           hint(show $sat__s_(%,%))
def $sat_s_(N, i) = $(-2^(N-1))     -- if i < $(-2^(N-1))
def $sat_s_(N, i) = $(2^(N-1) - 1)  -- if i > $(2^(N-1) - 1)
def $sat_s_(N, i) = i               -- otherwise


;; Integer operations

def $ineg_(N, iN(N)) : iN(N)
def $iabs_(N, iN(N)) : iN(N)
def $iclz_(N, iN(N)) : iN(N)
def $ictz_(N, iN(N)) : iN(N)
def $ipopcnt_(N, iN(N)) : iN(N)
def $iextend_(N, M, sx, iN(N)) : iN(N)          hint(show $iextend_((%,%))^(%)#((%)))

def $iadd_(N, iN(N), iN(N)) : iN(N)
def $isub_(N, iN(N), iN(N)) : iN(N)
def $imul_(N, iN(N), iN(N)) : iN(N)
def $idiv_(N, sx, iN(N), iN(N)) : iN(N)?        hint(show $idiv__#%2#$_(%1,%3,%4))
def $irem_(N, sx, iN(N), iN(N)) : iN(N)?        hint(show $irem__#%2#$_(%1,%3,%4))
def $imin_(N, sx, iN(N), iN(N)) : iN(N)         hint(show $imin__#%2#$_(%1,%3,%4))
def $imax_(N, sx, iN(N), iN(N)) : iN(N)         hint(show $imax__#%2#$_(%1,%3,%4))
def $iadd_sat_(N, sx, iN(N), iN(N)) : iN(N)     hint(show $iadd__sat__#%2#$_(%1,%3,%4))
def $isub_sat_(N, sx, iN(N), iN(N)) : iN(N)     hint(show $isub__sat__#%2#$_(%1,%3,%4))
def $iq15mulr_sat_(N, sx, iN(N), iN(N)) : iN(N) hint(show $iq15mulr__sat__#%2#$_(%1,%3,%4))
def $irelaxed_q15mulr_(N, sx, iN(N), iN(N)) : iN(N)* hint(show $relaxed__iq15mulr__#%2#$_(%1,%3,%4))
def $iavgr_(N, sx, iN(N), iN(N)) : iN(N)        hint(show $iavgr__#%2#$_(%1,%3,%4))

def $inot_(N, iN(N)) : iN(N)
def $irev_(N, iN(N)) : iN(N)
def $iand_(N, iN(N), iN(N)) : iN(N)
def $iandnot_(N, iN(N), iN(N)) : iN(N)
def $ior_(N, iN(N), iN(N)) : iN(N)
def $ixor_(N, iN(N), iN(N)) : iN(N)
def $ishl_(N, iN(N), u32) : iN(N)
def $ishr_(N, sx, iN(N), u32) : iN(N)    hint(show $ishr__#%2#$_(%1,%3,%4))
def $irotl_(N, iN(N), iN(N)) : iN(N)
def $irotr_(N, iN(N), iN(N)) : iN(N)

def $ibitselect_(N, iN(N), iN(N), iN(N)) : iN(N)
def $irelaxed_laneselect_(N, iN(N), iN(N), iN(N)) : iN(N)*

def $ieqz_(N, iN(N)) : u32
def $inez_(N, iN(N)) : u32

def $ieq_(N, iN(N), iN(N)) : u32
def $ine_(N, iN(N), iN(N)) : u32
def $ilt_(N, sx, iN(N), iN(N)) : u32     hint(show $ilt__#%2#$_(%1,%3,%4))
def $igt_(N, sx, iN(N), iN(N)) : u32     hint(show $igt__#%2#$_(%1,%3,%4))
def $ile_(N, sx, iN(N), iN(N)) : u32     hint(show $ile__#%2#$_(%1,%3,%4))
def $ige_(N, sx, iN(N), iN(N)) : u32     hint(show $ige__#%2#$_(%1,%3,%4))


;; TODO(3, rossberg): implement all numerics internally

def $ineg_(N, i_1) = $((2^N - i_1) \ 2^N)

def $iabs_(N, i_1) = i_1             -- if $signed_(N, i_1) >= 0
def $iabs_(N, i_1) = $ineg_(N, i_1)  -- otherwise

def $iclz_ hint(builtin)
;;def $iclz_(N, i_1) = k               -- if $ibits_(N, i_1) = (0)^k 1 d*

def $ictz_ hint(builtin)
;;def $ictz_(N, i_1) = k               -- if $ibits_(N, i_1) = d* 1 (0)^k

def $ipopcnt_ hint(builtin)
;;def $ipopcnt_(N, i_1) = k            -- if $ibits_(N, i_1) = (0* 1)^k 0*

def $iextend_(N, M, U, i) = $(i \ 2^M)
def $iextend_(N, M, S, i) = $inv_signed_(N, $signed_(M, $(i \ 2^M)))


def $iadd_(N, i_1, i_2) = $((i_1 + i_2) \ 2^N)

def $isub_(N, i_1, i_2) = $((2^N + i_1 - i_2) \ 2^N)

def $imul_(N, i_1, i_2) = $((i_1 * i_2) \ 2^N)

def $idiv_(N, U, i_1, 0)   = eps
def $idiv_(N, U, i_1, i_2) = $truncz($(i_1 / i_2))
def $idiv_(N, S, i_1, 0)   = eps
def $idiv_(N, S, i_1, i_2) = eps  -- if $($signed_(N, i_1) / $signed_(N, i_2)) = $rat$(2^(N-1))
def $idiv_(N, S, i_1, i_2) = $inv_signed_(N, $truncz($($signed_(N, i_1) / $signed_(N, i_2))))

def $irem_(N, U, i_1, 0)   = eps
def $irem_(N, U, i_1, i_2) = $(i_1 - i_2 * $truncz($(i_1 / i_2)))
def $irem_(N, S, i_1, 0)   = eps
def $irem_(N, S, i_1, i_2) = $inv_signed_(N, $(j_1 - j_2 * $truncz($(j_1 / j_2))))
                             -- if j_1 = $signed_(N, i_1) /\ j_2 = $signed_(N, i_2)

def $imin_(N, U, i_1, i_2) = i_1  -- if i_1 <= i_2
def $imin_(N, U, i_1, i_2) = i_2  -- if i_1 > i_2
def $imin_(N, S, i_1, i_2) = i_1  -- if $signed_(N, i_1) <= $signed_(N, i_2)
def $imin_(N, S, i_1, i_2) = i_2  -- otherwise

def $imax_(N, U, i_1, i_2) = i_1  -- if i_1 >= i_2
def $imax_(N, U, i_1, i_2) = i_2  -- if i_1 < i_2
def $imax_(N, S, i_1, i_2) = i_1  -- if $signed_(N, i_1) >= $signed_(N, i_2)
def $imax_(N, S, i_1, i_2) = i_2  -- otherwise

def $iadd_sat_(N, U, i_1, i_2) = $sat_u_(N, $(i_1 + i_2))
def $iadd_sat_(N, S, i_1, i_2) = $inv_signed_(N, $sat_s_(N, $($signed_(N, i_1) + $signed_(N, i_2))))

def $isub_sat_(N, U, i_1, i_2) = $sat_u_(N, $(i_1 - i_2))
def $isub_sat_(N, S, i_1, i_2) = $inv_signed_(N, $sat_s_(N, $($signed_(N, i_1) - $signed_(N, i_2))))

def $iq15mulr_sat_     hint(builtin)
def $irelaxed_q15mulr_ hint(builtin)
def $iavgr_            hint(builtin)

def $inot_             hint(builtin)
def $irev_             hint(builtin)
def $iand_             hint(builtin)
def $iandnot_          hint(builtin)
def $ior_              hint(builtin)
def $ixor_             hint(builtin)
def $ishl_             hint(builtin)
def $ishr_             hint(builtin)
def $irotl_            hint(builtin)
def $irotr_            hint(builtin)

def $ibitselect_          hint(builtin)
def $irelaxed_laneselect_ hint(builtin)


def $ieqz_(N, i_1) = $bool(i_1 = 0)

def $inez_(N, i_1) = $bool(i_1 =/= 0)


def $ieq_(N, i_1, i_2) = $bool(i_1 = i_2)

def $ine_(N, i_1, i_2) = $bool(i_1 =/= i_2)

def $ilt_(N, U, i_1, i_2) = $bool(i_1 < i_2)
def $ilt_(N, S, i_1, i_2) = $bool($signed_(N, i_1) < $signed_(N, i_2))

def $igt_(N, U, i_1, i_2) = $bool(i_1 > i_2)
def $igt_(N, S, i_1, i_2) = $bool($signed_(N, i_1) > $signed_(N, i_2))

def $ile_(N, U, i_1, i_2) = $bool(i_1 <= i_2)
def $ile_(N, S, i_1, i_2) = $bool($signed_(N, i_1) <= $signed_(N, i_2))

def $ige_(N, U, i_1, i_2) = $bool(i_1 >= i_2)
def $ige_(N, S, i_1, i_2) = $bool($signed_(N, i_1) >= $signed_(N, i_2))


;; Floating-point operations

def $fabs_(N, fN(N)) : fN(N)*
def $fneg_(N, fN(N)) : fN(N)*
def $fsqrt_(N, fN(N)) : fN(N)*
def $fceil_(N, fN(N)) : fN(N)*
def $ffloor_(N, fN(N)) : fN(N)*
def $ftrunc_(N, fN(N)) : fN(N)*
def $fnearest_(N, fN(N)) : fN(N)*

def $fadd_(N, fN(N), fN(N)) : fN(N)*
def $fsub_(N, fN(N), fN(N)) : fN(N)*
def $fmul_(N, fN(N), fN(N)) : fN(N)*
def $fdiv_(N, fN(N), fN(N)) : fN(N)*
def $fmin_(N, fN(N), fN(N)) : fN(N)*
def $fmax_(N, fN(N), fN(N)) : fN(N)*
def $fpmin_(N, fN(N), fN(N)) : fN(N)*
def $fpmax_(N, fN(N), fN(N)) : fN(N)*
def $frelaxed_min_(N, fN(N), fN(N)) : fN(N)*    hint(show $frelaxed__min_(%,%,%))
def $frelaxed_max_(N, fN(N), fN(N)) : fN(N)*    hint(show $frelaxed__max_(%,%,%))
def $fcopysign_(N, fN(N), fN(N)) : fN(N)*

def $feq_(N, fN(N), fN(N)) : u32
def $fne_(N, fN(N), fN(N)) : u32
def $flt_(N, fN(N), fN(N)) : u32
def $fgt_(N, fN(N), fN(N)) : u32
def $fle_(N, fN(N), fN(N)) : u32
def $fge_(N, fN(N), fN(N)) : u32

def $frelaxed_madd_(N, fN(N), fN(N), fN(N)) : fN(N)*    hint(show $frelaxed__madd_(%,%,%))
def $frelaxed_nmadd_(N, fN(N), fN(N), fN(N)) : fN(N)*   hint(show $frelaxed__nmadd_(%,%,%))


;; TODO(3, rossberg): implement numerics internally

def $fabs_     hint(builtin)
def $fneg_     hint(builtin)
def $fsqrt_    hint(builtin)
def $fceil_    hint(builtin)
def $ffloor_   hint(builtin)
def $ftrunc_   hint(builtin)
def $fnearest_ hint(builtin)

def $fadd_     hint(builtin)
def $fsub_     hint(builtin)
def $fmul_     hint(builtin)
def $fdiv_     hint(builtin)
def $fmin_     hint(builtin)
def $fmax_     hint(builtin)
def $fpmin_    hint(builtin)
def $fpmax_    hint(builtin)
def $frelaxed_min_ hint(builtin)
def $frelaxed_max_ hint(builtin)
def $fcopysign_    hint(builtin)

def $feq_ hint(builtin)
def $fne_ hint(builtin)
def $flt_ hint(builtin)
def $fgt_ hint(builtin)
def $fle_ hint(builtin)
def $fge_ hint(builtin)

def $frelaxed_madd_  hint(builtin)
def $frelaxed_nmadd_ hint(builtin)


;; Conversions

def $wrap__(M, N, iN(M)) : iN(N)
def $extend__(M, N, sx, iN(M)) : iN(N)               hint(show $extend_((%,%))^(%)#((%)))
def $trunc__(M, N, sx, fN(M)) : iN(N)?               hint(show $trunc_((%,%))^(%)#((%)))
def $trunc_sat__(M, N, sx, fN(M)) : iN(N)?           hint(show $trunc__sat_((%,%))^(%)#((%)))
def $relaxed_trunc__(M, N, sx, fN(M)) : iN(N)?       hint(show $relaxed__trunc_((%,%))^(%)#((%)))
def $demote__(M, N, fN(M)) : fN(N)*
def $promote__(M, N, fN(M)) : fN(N)*
def $convert__(M, N, sx, iN(M)) : fN(N)              hint(show $convert_((%,%))^(%)#((%)))
def $narrow__(M, N, sx, iN(M)) : iN(N)               hint(show $narrow_((%,%))^(%)#(%))

def $reinterpret__(numtype_1, numtype_2, num_(numtype_1)) : num_(numtype_2)


;; TODO(3, rossberg): implement numerics internally

def $wrap__          hint(builtin)
def $extend__        hint(builtin)
def $trunc__         hint(builtin)
def $trunc_sat__     hint(builtin)
def $relaxed_trunc__ hint(builtin)
def $demote__        hint(builtin)
def $promote__       hint(builtin)
def $convert__       hint(builtin)
def $narrow__        hint(builtin)

def $reinterpret__   hint(builtin)


;; Packed numbers

def $lpacknum_(lanetype, num_($lunpack(lanetype))) : lane_(lanetype)
    hint(show $pack_(%,%)) hint(macro "packnum")
def $cpacknum_(storagetype, lit_($cunpack(storagetype))) : lit_(storagetype)
    hint(show $pack_(%,%)) hint(macro "packnum")

def $lpacknum_(numtype, c) = c
def $lpacknum_(packtype, c) = $wrap__($size($lunpack(packtype)), $psize(packtype), c)

def $cpacknum_(consttype, c) = c
def $cpacknum_(packtype, c) = $wrap__($size($lunpack(packtype)), $psize(packtype), c)


def $lunpacknum_(lanetype, lane_(lanetype)) : num_($lunpack(lanetype))
    hint(show $unpack_(%,%)) hint(macro "unpacknum")
def $cunpacknum_(storagetype, lit_(storagetype)) : lit_($cunpack(storagetype))
    hint(show $unpack_(%,%)) hint(macro "unpacknum")

def $lunpacknum_(numtype, c) = c
def $lunpacknum_(packtype, c) = $extend__($psize(packtype), $size($lunpack(packtype)), U, c)

def $cunpacknum_(consttype, c) = c
def $cunpacknum_(packtype, c) = $extend__($psize(packtype), $size($lunpack(packtype)), U, c)


;; Operator dispatch

def $unop_(numtype, unop_(numtype), num_(numtype)) : num_(numtype)*
    hint(show %2#$_(%1,%3))
def $binop_(numtype, binop_(numtype), num_(numtype), num_(numtype)) : num_(numtype)*
    hint(show %2#$_(%1,%3, %4))
def $testop_(numtype, testop_(numtype), num_(numtype)) : u32
    hint(show %2#$_(%1,%3))
def $relop_(numtype, relop_(numtype), num_(numtype), num_(numtype)) : u32
    hint(show %2#$_(%1,%3, %4))
def $cvtop__(numtype_1, numtype_2, cvtop__(numtype_1, numtype_2), num_(numtype_1)) : num_(numtype_2)*
    hint(show %3#$__(%1,%2,%4))

def $unop_(Inn, CLZ, i) = $iclz_($sizenn(Inn), i)
def $unop_(Inn, CTZ, i) = $ictz_($sizenn(Inn), i)
def $unop_(Inn, POPCNT, i) = $ipopcnt_($sizenn(Inn), i)
def $unop_(Inn, EXTEND M, i) = $iextend_($sizenn(Inn), M, S, i)

def $unop_(Fnn, ABS, f) = $fabs_($sizenn(Fnn), f)
def $unop_(Fnn, NEG, f) = $fneg_($sizenn(Fnn), f)
def $unop_(Fnn, SQRT, f) = $fsqrt_($sizenn(Fnn), f)
def $unop_(Fnn, CEIL, f) = $fceil_($sizenn(Fnn), f)
def $unop_(Fnn, FLOOR, f) = $ffloor_($sizenn(Fnn), f)
def $unop_(Fnn, TRUNC, f) = $ftrunc_($sizenn(Fnn), f)
def $unop_(Fnn, NEAREST, f) = $fnearest_($sizenn(Fnn), f)

def $binop_(Inn, ADD, i_1, i_2) = $iadd_($sizenn(Inn), i_1, i_2)
def $binop_(Inn, SUB, i_1, i_2) = $isub_($sizenn(Inn), i_1, i_2)
def $binop_(Inn, MUL, i_1, i_2) = $imul_($sizenn(Inn), i_1, i_2)
def $binop_(Inn, DIV sx, i_1, i_2) = $idiv_($sizenn(Inn), sx, i_1, i_2)
def $binop_(Inn, REM sx, i_1, i_2) = $irem_($sizenn(Inn), sx, i_1, i_2)
def $binop_(Inn, AND, i_1, i_2) = $iand_($sizenn(Inn), i_1, i_2)
def $binop_(Inn, OR, i_1, i_2) = $ior_($sizenn(Inn), i_1, i_2)
def $binop_(Inn, XOR, i_1, i_2) = $ixor_($sizenn(Inn), i_1, i_2)
def $binop_(Inn, SHL, i_1, i_2) = $ishl_($sizenn(Inn), i_1, i_2)
def $binop_(Inn, SHR sx, i_1, i_2) = $ishr_($sizenn(Inn), sx, i_1, i_2)
def $binop_(Inn, ROTL, i_1, i_2) = $irotl_($sizenn(Inn), i_1, i_2)
def $binop_(Inn, ROTR, i_1, i_2) = $irotr_($sizenn(Inn), i_1, i_2)

def $binop_(Fnn, ADD, f_1, f_2) = $fadd_($sizenn(Fnn), f_1, f_2)
def $binop_(Fnn, SUB, f_1, f_2) = $fsub_($sizenn(Fnn), f_1, f_2)
def $binop_(Fnn, MUL, f_1, f_2) = $fmul_($sizenn(Fnn), f_1, f_2)
def $binop_(Fnn, DIV, f_1, f_2) = $fdiv_($sizenn(Fnn), f_1, f_2)
def $binop_(Fnn, MIN, f_1, f_2) = $fmin_($sizenn(Fnn), f_1, f_2)
def $binop_(Fnn, MAX, f_1, f_2) = $fmax_($sizenn(Fnn), f_1, f_2)
def $binop_(Fnn, COPYSIGN, f_1, f_2) = $fcopysign_($sizenn(Fnn), f_1, f_2)

def $testop_(Inn, EQZ, i) = $ieqz_($sizenn(Inn), i)

def $relop_(Inn, EQ, i_1, i_2) = $ieq_($sizenn(Inn), i_1, i_2)
def $relop_(Inn, NE, i_1, i_2) = $ine_($sizenn(Inn), i_1, i_2)
def $relop_(Inn, LT sx, i_1, i_2) = $ilt_($sizenn(Inn), sx, i_1, i_2)
def $relop_(Inn, GT sx, i_1, i_2) = $igt_($sizenn(Inn), sx, i_1, i_2)
def $relop_(Inn, LE sx, i_1, i_2) = $ile_($sizenn(Inn), sx, i_1, i_2)
def $relop_(Inn, GE sx, i_1, i_2) = $ige_($sizenn(Inn), sx, i_1, i_2)

def $relop_(Fnn, EQ, f_1, f_2) = $feq_($sizenn(Fnn), f_1, f_2)
def $relop_(Fnn, NE, f_1, f_2) = $fne_($sizenn(Fnn), f_1, f_2)
def $relop_(Fnn, LT, f_1, f_2) = $flt_($sizenn(Fnn), f_1, f_2)
def $relop_(Fnn, GT, f_1, f_2) = $fgt_($sizenn(Fnn), f_1, f_2)
def $relop_(Fnn, LE, f_1, f_2) = $fle_($sizenn(Fnn), f_1, f_2)
def $relop_(Fnn, GE, f_1, f_2) = $fge_($sizenn(Fnn), f_1, f_2)

def $cvtop__(Inn_1, Inn_2, EXTEND sx, i_1) =
  $extend__($sizenn1(Inn_1), $sizenn2(Inn_2), sx, i_1)
def $cvtop__(Inn_1, Inn_2, WRAP, i_1) =
  $wrap__($sizenn1(Inn_1), $sizenn2(Inn_2), i_1)
def $cvtop__(Fnn_1, Inn_2, TRUNC sx, f_1) =
  $trunc__($sizenn1(Fnn_1), $sizenn2(Inn_2), sx, f_1)
def $cvtop__(Fnn_1, Inn_2, TRUNC_SAT sx, f_1) =
  $trunc_sat__($sizenn1(Fnn_1), $sizenn2(Inn_2), sx, f_1)
def $cvtop__(Inn_1, Fnn_2, CONVERT sx, i_1) =
  $convert__($sizenn1(Inn_1), $sizenn2(Fnn_2), sx, i_1)
def $cvtop__(Fnn_1, Fnn_2, PROMOTE, f_1) =
  $promote__($sizenn1(Fnn_1), $sizenn2(Fnn_2), f_1)
def $cvtop__(Fnn_1, Fnn_2, DEMOTE, f_1) =
  $demote__($sizenn1(Fnn_1), $sizenn2(Fnn_2), f_1)
def $cvtop__(Inn_1, Fnn_2, REINTERPRET, i_1) =
  $reinterpret__(Inn_1, Fnn_2, i_1) -- if $size(Inn_1) = $size(Fnn_2)  ;; TODO(3, rossberg): make implicit
def $cvtop__(Fnn_1, Inn_2, REINTERPRET, f_1) =
  $reinterpret__(Fnn_1, Inn_2, f_1) -- if $size(Fnn_1) = $size(Inn_2)  ;; TODO(3, rossberg): make implicit
